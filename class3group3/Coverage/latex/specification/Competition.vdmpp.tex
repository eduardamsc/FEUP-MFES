\begin{vdmpp}[breaklines=true]
class Competition is subclass of Activity
/*
  Defines a competition which is an activity with users as competitors
  and a winner once the activity is completed.
  Daniel Marques & Eduarda Cunha, FEUP, MFES, 2018/19.
*/

instance variables
 public m_competitors : set of User;
 public m_winner : [User];
 
 inv forall u1, u2 in set m_competitors & u1 <> u2 => u1.m_username <> u2.m_username;
 inv m_winner = nil or (m_has_ended and m_winner in set m_competitors); -- Winner must be a competitor

operations
 -- Competition constructor
(*@
\label{Competition:17}
@*)
 public Competition : String * String * rat * Brand ==> Competition
 Competition(title, description, prize, brand) ==
 (
  m_competitors := {};
  m_winner := nil;
  Activity(title, description, prize, brand);
 );
 
 -- Add a competitor
(*@
\label{addCompetitor:26}
@*)
 public addCompetitor : User ==> ()
 addCompetitor(user) == 
 (
  m_competitors := m_competitors union {user}
 )
 pre user not in set m_competitors and not m_has_started
 post m_competitors = m_competitors~ union {user};
 
 -- Choose winner
(*@
\label{chooseWinner:35}
@*)
 public chooseWinner : User ==> ()
 chooseWinner(user) ==
 (
  m_winner := user;
 )
 pre m_has_started and m_has_ended and m_winner = nil and user in set m_competitors
 post m_winner = user and m_winner in set m_competitors;

end Competition
\end{vdmpp}
\bigskip
\begin{longtable}{|l|r|r|r|}
\hline
Function or operation & Line & Coverage & Calls \\
\hline
\hline
\hyperref[Competition:17]{Competition} & 17&100.0\% & 2 \\
\hline
\hyperref[addCompetitor:26]{addCompetitor} & 26&100.0\% & 4 \\
\hline
\hyperref[chooseWinner:35]{chooseWinner} & 35&100.0\% & 1 \\
\hline
\hline
Competition.vdmpp & & 100.0\% & 7 \\
\hline
\end{longtable}

